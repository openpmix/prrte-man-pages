# -*- shell-script -*-
#
# Copyright (c) 2004-2009 The Trustees of Indiana University and Indiana
#                         University Research and Technology
#                         Corporation.  All rights reserved.
# Copyright (c) 2004-2010 The University of Tennessee and The University
#                         of Tennessee Research Foundation.  All rights
#                         reserved.
# Copyright (c) 2004-2007 High Performance Computing Center Stuttgart,
#                         University of Stuttgart.  All rights reserved.
# Copyright (c) 2004-2005 The Regents of the University of California.
#                         All rights reserved.
# Copyright (c) 2006-2020 Cisco Systems, Inc.  All rights reserved
# Copyright (c) 2006-2008 Sun Microsystems, Inc.  All rights reserved.
# Copyright (c) 2006-2017 Los Alamos National Security, LLC.  All rights
#                         reserved.
# Copyright (c) 2009      Oak Ridge National Labs.  All rights reserved.
# Copyright (c) 2011-2013 NVIDIA Corporation.  All rights reserved.
# Copyright (c) 2012      Oracle and/or its affiliates.  All rights reserved.
# Copyright (c) 2013      Mellanox Technologies, Inc.
#                         All rights reserved.
# Copyright (c) 2014-2020 Intel, Inc.  All rights reserved.
# Copyright (c) 2016      IBM Corporation.  All rights reserved.
# Copyright (c) 2016-2018 Research Organization for Information Science
#                         and Technology (RIST). All rights reserved.
# Copyright (c) 2021      Nanook Consulting.  All rights reserved.
# $COPYRIGHT$
#
# Additional copyrights may follow
#
# $HEADER$
#


############################################################################
# Initialization, version number, and other random setup/init stuff
############################################################################

# We don't have the version number to put in here yet, and we can't
# call PRTE_GET_VERSION (etc.) before AC_INIT.  So use the shell
# version.

AC_INIT([prte-man-pages],[m4_normalize(esyscmd([config/prte_get_version.sh VERSION --tarball]))],[https://github.com/openpmix/prrte/issues],[prrte])
AC_PREREQ([2.69])
AC_CONFIG_AUX_DIR(./config)
# Note that this directory must *exactly* match what was specified via
# -I in ACLOCAL_AMFLAGS in the top-level Makefile.am.
AC_CONFIG_MACRO_DIR(./config)

PRTE_TOP_BUILDDIR="`pwd`"
AC_SUBST(PRTE_TOP_BUILDDIR)
top_buildir=`pwd`
cd "$srcdir"
PRTE_TOP_SRCDIR="`pwd`"
top_srcdir=$PRTE_TOP_SRCDIR
AC_SUBST(PRTE_TOP_SRCDIR)
cd "$PRTE_TOP_BUILDDIR"

AC_MSG_NOTICE([builddir: $PRTE_TOP_BUILDDIR])
AC_MSG_NOTICE([srcdir: $PRTE_TOP_SRCDIR])
if test "$PRTE_TOP_BUILDDIR" != "$PRTE_TOP_SRCDIR"; then
    AC_MSG_NOTICE([Detected VPATH build])
fi

# This must be before AM_INIT_AUTOMAKE
AC_CANONICAL_TARGET

# Init automake
AM_INIT_AUTOMAKE([foreign dist-bzip2 subdir-objects no-define 1.13.4])

# SILENT_RULES is new in AM 1.11, but we require 1.13.4 or higher via
# autogen.  Limited testing shows that calling SILENT_RULES directly
# works in more cases than adding "silent-rules" to INIT_AUTOMAKE
# (even though they're supposed to be identical).  Shrug.
AM_SILENT_RULES([yes])

# set the language
AC_LANG([C])

####################################################################
# Setup the configure header files
####################################################################

AH_TOP([/* -*- c -*-
 *
 * Copyright (c) 2021      Nanook Consulting.  All rights reserved.
 * $COPYRIGHT$
 *
 * Additional copyrights may follow
 *
 * $HEADER$
 *
 * This file is automatically generated by configure.  Edits will be lost
 * the next time you run configure!
 */

#ifndef PRTE_MANPAGES_CONFIG_H
#define PRTE_MANPAGES_CONFIG_H

])
AH_BOTTOM([
#endif /* PRTE_MANPAGES_CONFIG_H */
])

############################################################################
# Setup Libtool
############################################################################

# We want new Libtool.  None of that old stuff.  Pfft.
m4_ifdef([LT_PREREQ], [],
         [m4_fatal([libtool version 2.2.6 or higher is required], [63])])
LT_PREREQ([2.2.6])

# Setup libtool, but disable F77, Java and Windows Resource
# Compiler support -- we don't need that stuff.
AC_ENABLE_SHARED
AC_DISABLE_STATIC

# This did not exist pre AM 1.11.x (where x is somewhere >0 and <3),
# but it is necessary in AM 1.12.x.
m4_ifdef([AM_PROG_AR], [AM_PROG_AR])

LT_INIT()
LT_LANG([C])

PRTE_MANPAGES_SETUP_PANDOC

AC_CONFIG_FILES([Makefile])

AC_OUTPUT
